#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбновитьВидимостьАрхива();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПодключитьОбработчикОжидания("ОбработатьВсеДокументыНаКлиенте", ПолучитьИнтервалАвтоматическойОбработки() * 60, Ложь);
	
КонецПроцедуры
#КонецОбласти
#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура ПоказыватьАрхивПриИзменении(Элемент)
	
	ОбновитьВидимостьАрхива();
	
КонецПроцедуры
#КонецОбласти
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПроверитьКаталоги(Команда)
	
	ПС_ИндексацияФайловКлиент.ПроверитьКаталогиСФайлами();
	ПС_ИндексацияФайловВызовСервера.ПроверитьКаталогиСФайлами();
	
	Элементы.Список.Обновить();
	
КонецПроцедуры


&НаКлиенте
Процедура ОбработатьВыбранныйФайл(Команда)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Состояние("Выполняется обработка документа");
	ПС_ИндексацияФайловКлиент.ОбработатьДокумент(ТекущиеДанные.ХешСумма, Истина);
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВсеФайлы(Команда)
	
	ОбработатьВсеФайлыНаКлиенте(Истина);

КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработатьВсеФайлыНаКлиенте(Вручную)
	
	МассивВсехНезавершенных = ПолучитьВсеНеобработанныеДокументыНаКлиенте();
	
	КоличествоЭлементов = МассивВсехНезавершенных.Количество();
	
	ТекНомер = 0;
	
	Для каждого ТекХешСумма Из МассивВсехНезавершенных Цикл
		
		Состояние("Выполняется обработка документов", Цел(ТекНомер / КоличествоЭлементов * 100));
		ПС_ИндексацияФайловКлиент.ОбработатьДокумент(ТекХешСумма, Вручную);
		
		ТекНомер = ТекНомер + 1;
		
	КонецЦикла;
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВсеДокументыНаКлиенте() Экспорт

	Если АвтоматическаяОбработка Тогда
		
		ПС_ИндексацияФайловКлиент.ПроверитьКаталогиСФайлами();
		ПС_ИндексацияФайловВызовСервера.ПроверитьКаталогиСФайлами();
		ОбработатьВсеФайлыНаКлиенте(Ложь);

	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьИнтервалАвтоматическойОбработки()
	
	Возврат ПС_ИндексацияФайловВызовСервера.НастройкаРаспознаванияЧисло("ИнтервалАвтоматическойОбработкиМин", 1);
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьВсеНеобработанныеДокументыНаКлиенте()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПС_ОбработкаДокументов.ХешСумма КАК ХешСумма
	|ИЗ
	|	РегистрСведений.ПС_ОбработкаДокументов КАК ПС_ОбработкаДокументов
	|ГДЕ
	|	ПС_ОбработкаДокументов.ОбработкаЗавершена = ЛОЖЬ
	|	И ПС_ОбработкаДокументов.НаКлиенте = ИСТИНА";
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	МассивХешСумм = Новый Массив;
	
	Пока Выборка.Следующий() Цикл
		МассивХешСумм.Добавить(Выборка.ХешСумма);
	КонецЦикла;	
	
	Возврат МассивХешСумм;
	
КонецФункции

&НаСервере
Процедура ОбновитьВидимостьАрхива()
	
	Список.Параметры.УстановитьЗначениеПараметра("ПоказыватьАрхив", ПоказыватьАрхив);
	
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

//Обрабатывает документ по хеш сумме
//Параметры:
//	ХешСумма - Строка - Хеш сумма файла
Процедура ОбработатьДокумент(ХешСумма, АдресФайлаНаКлиенте = "") Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ПС_ОбработкаДокументов.Период КАК Период,
	|	ПС_ОбработкаДокументов.Объект КАК Объект,
	|	ПС_ОбработкаДокументов.ХешСумма КАК ХешСумма,
	|	ПС_ОбработкаДокументов.Распознан КАК Распознан,
	|	ПС_ОбработкаДокументов.Найден КАК Найден,
	|	ПС_ОбработкаДокументов.Прикреплен КАК Прикреплен,
	|	ПС_ОбработкаДокументов.КоличествоПопыток КАК КоличествоПопыток,
	|	ПС_ОбработкаДокументов.ИзвлеченныйТекст КАК ИзвлеченныйТекст,
	|	ПС_ОбработкаДокументов.Вероятность КАК Вероятность,
	|	ПС_ОбработкаДокументов.КлючевыеРеквизиты КАК КлючевыеРеквизиты,
	|	ПС_ОбработкаДокументов.ОбработкаЗавершена КАК ОбработкаЗавершена,
	|	ПС_ОбработкаДокументов.ИмяФайла КАК ИмяФайла,
	|	ПС_ОбработкаДокументов.НаКлиенте КАК НаКлиенте,
	|	ПС_ОбработкаДокументов.ПредставлениеДокумента КАК ПредставлениеДокумента,
	|	ПС_ОбработкаДокументов.ТипДокумента КАК ТипДокумента,
	|	ПС_ОбработкаДокументов.КомментарийОбработки КАК КомментарийОбработки
	|ИЗ
	|	РегистрСведений.ПС_ОбработкаДокументов КАК ПС_ОбработкаДокументов
	|ГДЕ
	|	ПС_ОбработкаДокументов.ХешСумма = &ХешСумма
	|	И НЕ ПС_ОбработкаДокументов.ОбработкаЗавершена";
	
	Запрос.УстановитьПараметр("ХешСумма", ХешСумма);
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	
	Если Выборка.Следующий() Тогда
	
		Если НЕ Выборка.Распознан Тогда
			
			РезультатРаспознавания = РаспознатьДокумент(Выборка, АдресФайлаНаКлиенте);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Возвращает массив каталогов
//Параметры:
// КаталогиНаКлиенте - булево
// Возвращает массив Обрабатываемых каталогов с полями:
//	ПутьНаСервере - строка
//	НаКлиенте - булево
//Возвращаемое значение:
//	Тип: массив структур
Функция ПолучитьИндексируемыеКаталоги(КаталогиНаКлиенте = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПС_КаталогиПотоковогоСканирования.Ссылка КАК Ссылка,
		|	ПС_КаталогиПотоковогоСканирования.ПутьККаталогу КАК ПутьККаталогу,
		|	ПС_КаталогиПотоковогоСканирования.НаКлиенте КАК НаКлиенте
		|ИЗ
		|	Справочник.ПС_КаталогиПотоковогоСканирования КАК ПС_КаталогиПотоковогоСканирования
		|ГДЕ
		|	ПС_КаталогиПотоковогоСканирования.Активен
		|	И
		|		ПС_КаталогиПотоковогоСканирования.Ссылка <> ЗНАЧЕНИЕ(Справочник.ПС_КаталогиПотоковогоСканирования.КаталогДляОбработанныхДокументов)
		|	И (&КаталогиНаКлиенте = НЕОПРЕДЕЛЕНО
		|	ИЛИ ПС_КаталогиПотоковогоСканирования.НаКлиенте = &КаталогиНаКлиенте)
		|
		|УПОРЯДОЧИТЬ ПО
		|	НаКлиенте";
	
	Запрос.УстановитьПараметр("КаталогиНаКлиенте", КаталогиНаКлиенте);
	
	ВыборкаКаталогов = Запрос.Выполнить().Выбрать();
	
	Результат = Новый Массив;
	
	Пока ВыборкаКаталогов.Следующий() Цикл
		
		СтруктураКаталога = Новый Структура;
		СтруктураКаталога.Вставить("ПутьККаталогу", ВыборкаКаталогов.ПутьККаталогу);
		СтруктураКаталога.Вставить("НаКлиенте", ВыборкаКаталогов.НаКлиенте);
		
		Результат.Добавить(СтруктураКаталога);
		
	КонецЦикла;
	
	Возврат Результат;	
		
КонецФункции


//Перебирает файлы в каталогах сканирования и заносит в регистр
Процедура ПроверитьКаталогиСФайлами() Экспорт
	
	МассивКаталогов = ПолучитьИндексируемыеКаталоги(Ложь);
	
	Для каждого ВыборкаКаталогов Из МассивКаталогов Цикл
		
		ПутьККаталогу = ВыборкаКаталогов.ПутьККаталогу;		
		КаталогНаСервере = Новый Файл(ПутьККаталогу);
		
		Если НЕ КаталогНаСервере.Существует() Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		МассивФайлов = НайтиФайлы(ПутьККаталогу, "*.*", Истина);
		
		Для Каждого Файл Из МассивФайлов Цикл
			
			Если ЭтоИзображениеИлиPDF(Файл.Расширение) Тогда
	
				ХешСумма = ПолучитьХешСуммуФайла(Файл.ПолноеИмя);

				ДобавитьФайлВРегистр(Файл.ПолноеИмя, ХешСумма);

			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

//Добавляет файл в регистр
//Параметры:
//	ПутьКФайлу - строка
//	ХешСумма - строка
//	ФайлНаКлиенте - булево
Процедура ДобавитьФайлВРегистр(ПутьКФайлу, ХешСумма, ФайлНаКлиенте = Ложь) Экспорт

	НаборЗаписей = РегистрыСведений.ПС_ОбработкаДокументов.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ХешСумма.Установить(ХешСумма);
	НаборЗаписей.Прочитать();
				
	Если НаборЗаписей.Количество() = 0 Тогда
				
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.Период = ТекущаяДатаСеанса();
		НоваяЗапись.ХешСумма = ХешСумма;
		НоваяЗапись.ИмяФайла = ПутьКФайлу;
		НоваяЗапись.НаКлиенте = ФайлНаКлиенте;
					
		НаборЗаписей.Записать();
					
	КонецЕсли;	
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция РаспознатьДокумент(Выборка, АдресФайлаНаКлиенте = "")
	
	НужноУдалитьФайл = Ложь;
	
	Если Выборка.НаКлиенте И ЭтоАдресВременногоХранилища(АдресФайлаНаКлиенте) Тогда
		
		ИмяВременногоФайла = ПолучитьИмяВременногоФайла();
		ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресФайлаНаКлиенте);
		ДвоичныеДанные.Записать(АдресФайлаНаКлиенте);
		ПолноеИмяФайла = ИмяВременногоФайла;
		
		НужноУдалитьФайл = Истина;
		
	Иначе

		ПолноеИмяФайла = Выборка.ИмяФайла;
				
	КонецЕсли;
	
	РезультатОбработки = Новый Структура("ОбработанУспешно,Комментарий", Ложь, "");
	
	Если НужноУдалитьФайл Тогда
		УдалитьФайлы(ИмяВременногоФайла);
	КонецЕсли;	
	
	Возврат РезультатОбработки;
	
КонецФункции

Функция ЭтоИзображениеИлиPDF(Расширение) Экспорт
		
	// Проверяем, является ли файл изображением или PDF файлом
	МассивДопустимыхРасширений = Новый Массив;
	МассивДопустимыхРасширений.Добавить(".jpg");
	МассивДопустимыхРасширений.Добавить(".jpeg");
	МассивДопустимыхРасширений.Добавить(".png");
	МассивДопустимыхРасширений.Добавить(".pdf");
	
	Возврат МассивДопустимыхРасширений.Найти(Расширение) <> Неопределено;
	
КонецФункции

Функция ПолучитьХешСуммуФайла(Знач ПолноеИмяФайла) Экспорт
	
	НужноУдалитьФайл = Ложь;
	
	Если ЭтоАдресВременногоХранилища(ПолноеИмяФайла) Тогда
		
		ИмяВременногоФайла = ПолучитьИмяВременногоФайла();
		ДвоичныеДанные = ПолучитьИзВременногоХранилища(ПолноеИмяФайла);
		ДвоичныеДанные.Записать(ИмяВременногоФайла);
		ПолноеИмяФайла = ИмяВременногоФайла;
		
		НужноУдалитьФайл = Истина;
		
	КонецЕсли;
	
	Хеш = Новый ХешированиеДанных(ХешФункция.MD5);
	Хеш.ДобавитьФайл(ПолноеИмяФайла);
	ХешСумма = Строка(Хеш.ХешСумма);
	
	Если НужноУдалитьФайл Тогда
		УдалитьФайлы(ИмяВременногоФайла);
	КонецЕсли;
	
	Возврат ХешСумма;
	
КонецФункции

#КонецОбласти

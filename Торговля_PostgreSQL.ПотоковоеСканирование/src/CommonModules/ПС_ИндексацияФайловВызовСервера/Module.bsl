
#Область ПрограммныйИнтерфейс

// Возвращает значение параметра распознавания
// Параметры:
//	ИмяНастройки - Строка - Имя настройки
// Возвращаемое значение:
//	Строка - Значение настройки
//
Функция ПолучитьНастройкуРаспознавания(ИмяНастройки) Экспорт

	Возврат Справочники.ПС_НастройкиРаспознавания.ПараметрРаспознавания(ИмяНастройки);
	
КонецФункции

//Переупаковывает PDF файл
//Параметры:
//	МассивДанных - Массив структур
//Возвращаемое значение:
//	Строка - Адрес файла на сервере
Функция ПереупаковатьPDF(МассивДанных) Экспорт
	
	Макет = ПолучитьОбщийМакет("МакетДляСборки");
	ТабДок = Новый ТабличныйДокумент;
	
	Для каждого ТекЭлемент Из МассивДанных Цикл
			
		ДвоичныеДанные = ТекЭлемент.Картинка;
			
		Если ДвоичныеДанные <> Неопределено Тогда
				
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
			Область = Макет.ПолучитьОбласть("Изображение");
			Рисунок = Область.Рисунки.Добавить(ТипРисункаТабличногоДокумента.Картинка);
			Рисунок.Высота = 297;
			Рисунок.Ширина = 210;
			Рисунок.РазмерКартинки = РазмерКартинки.Пропорционально;
			Рисунок.ГраницаСверху = Истина;
			Рисунок.ГраницаСнизу = Истина;
			Рисунок.ГраницаСправа = Истина;
			Рисунок.ГраницаСлева = Истина;
				
			Рисунок.Картинка = Новый Картинка(ДвоичныеДанные);
			Рисунок.Линия = Новый Линия(ТипЛинииРисункаТабличногоДокумента.Сплошная, 3);
			ТабДок.Вывести(Область);
				
		КонецЕсли;
		
	КонецЦикла;
	
	ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
	ОбластьТекст = Макет.ПолучитьОбласть("СтрокаТекста");
			
	Для каждого ТекЭлемент Из МассивДанных Цикл
			
        Текст = ТекЭлемент.Текст;
            
		Если Текст = Неопределено Тогда
				
			Продолжить;
				
		КонецЕсли;
			
		Для Счетчик = 1 По СтрЧислоСтрок(Текст) Цикл
				
			СтрокаТекст = СтрПолучитьСтроку(Текст,Счетчик);
			ОбластьТекст.Параметры.Текст = СокрЛП(СтрокаТекст);
			ТабДок.Вывести(ОбластьТекст);
				
		КонецЦикла;
			
		ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
			
	КонецЦикла;	
				
	ТабДок.АвтоМасштаб = истина;
	ТабДок.ШиринаСтраницы = 210;
	ТабличныйДокумент = ТабДок;	
		
	Возврат ТабличныйДокумент;
	
КонецФункции

//Обрабатывает документ по хеш сумме
//Параметры:
//	ХешСумма - Строка - Хеш сумма файла
//  АдресФайлаНаКлиенте - Строка - Адрес файла на клиенте
Процедура ОбработатьДокумент(ХешСумма, АдресФайлаНаКлиенте = "", ПрикрепитьДокументНаКлиенте = Ложь) Экспорт
	
	НаборЗаписейРегистра = РегистрыСведений.ПС_ОбработкаДокументов.СоздатьНаборЗаписей();
	НаборЗаписейРегистра.Отбор.ХешСумма.Установить(ХешСумма);
	НаборЗаписейРегистра.Прочитать();
	
	Если НаборЗаписейРегистра.Количество() > 0 Тогда
	
		ЗаписьРегистра = НаборЗаписейРегистра[0];
		
		ЗаписьРегистра.КомментарийОбработки = ЗаписьРегистра.КомментарийОбработки + ?(ПустаяСтрока(ЗаписьРегистра.КомментарийОбработки), "", Символы.ПС) 
		+ "[" + Строка(ТекущаяДатаСеанса()) + "]" + Символы.ПС;
		ЗаписьРегистра.КоличествоПопыток = НаборЗаписейРегистра[0].КоличествоПопыток + 1; 
	
		Если НЕ ЗаписьРегистра.Распознан Тогда
			
			РаспознатьДокумент(ЗаписьРегистра, АдресФайлаНаКлиенте);	
			НаборЗаписейРегистра.Записать();		
			
		КонецЕсли;
		
		Если ЗаписьРегистра.Распознан И НЕ ЗаписьРегистра.Найден Тогда
			
			НайтиСвязиПоКлючевымРеквизитам(ЗаписьРегистра);
			НаборЗаписейРегистра.Записать();
			
		КонецЕсли;
		
		Если ЗаписьРегистра.Найден И НЕ ЗаписьРегистра.Прикреплен Тогда
		
			ПорогПрикрепленияФайла = НастройкаРаспознаванияЧисло("ПорогТочностиДляПрикрепленияДокументов", 95);
			
			Если ЗаписьРегистра.Вероятность >= ПорогПрикрепленияФайла Тогда
			
				ЗаписьРегистра.КомментарийОбработки = ЗаписьРегистра.КомментарийОбработки 
				+ ?(ЗаписьРегистра.КомментарийОбработки  = "", "", Символы.ПС) + "Точность поиска достаточна, чтобы прикрепить скан документа в автоматическом режиме";			
			
				Если ЗаписьРегистра.НаКлиенте Тогда
					
					ПрикрепитьДокументНаКлиенте = Истина;
					
				Иначе
					
					ПрикрепитьСканДокументаНаСервере(ЗаписьРегистра, ЗаписьРегистра.ИмяФайла); //Прикрепляем файл, который хранится на сервере
					
				КонецЕсли;		
						
				НаборЗаписейРегистра.Записать();						
				
			КонецЕсли;
	
		КонецЕсли;
		
		Если ЗаписьРегистра.Прикреплен И НЕ ЗаписьРегистра.ОбработкаЗавершена Тогда
						
			Если НЕ ЗаписьРегистра.НаКлиенте Тогда						
						
				ЗавершитьОбработкуДокументаНаСервере(ЗаписьРегистра);
				
			КонецЕсли;
						
		КонецЕсли;
		
		//!!! После всех поисков нужно сверять суммы, контрагентов, даты, номера. Если документ найден, но совпадение не полное, подсвечивать
		
		НаборЗаписейРегистра.Записать();
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПрикрепитьИЗавершитьОбработкуДокумента(ХешСумма, АдресХранилища) Экспорт
	
	НаборЗаписейРегистра = РегистрыСведений.ПС_ОбработкаДокументов.СоздатьНаборЗаписей();
	НаборЗаписейРегистра.Отбор.ХешСумма.Установить(ХешСумма);
	НаборЗаписейРегистра.Прочитать();
	
	Если НаборЗаписейРегистра.Количество() > 0 Тогда
	
		ЗаписьРегистра = НаборЗаписейРегистра[0];
	
		Если НЕ ЗаписьРегистра.Прикреплен Тогда
	
			ПрикрепитьСканДокументаНаСервере(ЗаписьРегистра, АдресХранилища); //Прикрепляем файл, который хранится на сервере
						
			НаборЗаписейРегистра.Записать();
			
		КонецЕсли;
		
		Если ЗаписьРегистра.Прикреплен И НЕ ЗаписьРегистра.ОбработкаЗавершена Тогда					
						
			ЗавершитьОбработкуДокументаНаСервере(ЗаписьРегистра);
						
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

//Возвращает файл с сервера
//Параметры:
//	ИмяФайла - Строка - Имя файла
//Возвращаемое значение:
//	ДвоичныеДанные - Двоичные данные файла
Функция ПолучитьФайлССервера(ИмяФайла) Экспорт

	Возврат ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(ИмяФайла));
	
КонецФункции

//Возвращает файл с сервера
//Параметры:
//	ТипПлатформы - Строка - тип платформы клиента/сервера
//Возвращаемое значение:
//	ДвоичныеДанные - Двоичные данные файла
Функция ПолучитьКомпонентыКонвертацииССервера(ТипПлатформы, ПоместитьВХранилище = Истина) Экспорт
	
	Если СтрНайти(ТипПлатформы,"Windows x86-64") > 0 Тогда
		 
		ДвДанные = ПолучитьОбщийМакет("NativeComp_x64");
		
	ИначеЕсли СтрНайти(ТипПлатформы,"Windows") > 0 Тогда
		
		ДвДанные = ПолучитьОбщийМакет("NativeComp_x86");
		
	Иначе
		
		ВызватьИсключение "Платформа не поддерживается текущей компонентой";
		
	КонецЕсли;
	
	Если ПоместитьВХранилище Тогда

		Возврат ПоместитьВоВременноеХранилище(ДвДанные);
		
	Иначе
		
		Возврат ДвДанные;
		
	КонецЕсли;
	
КонецФункции
//Возвращает тип ОС
//Возвращаемое значение:
//Булево
Функция ЭтоСерверLinux() Экспорт
	
	СистемнаяИнформация = Новый СистемнаяИнформация;
	Возврат СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Linux_x86 ИЛИ СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Linux_x86_64;
	
КонецФункции

//Возвращает данные регистра по хеш сумме
//Параметры:
//	ХешСумма - Строка - Хеш сумма файла
//Возвращаемое значение:
//	РезультатЗапроса - Массив структур
Функция ПолучитьДанныеРегистраПоХешСумме(ХешСумма) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ПС_ОбработкаДокументов.Период КАК Период,
	|	ПС_ОбработкаДокументов.Объект КАК Объект,
	|	ПС_ОбработкаДокументов.ХешСумма КАК ХешСумма,
	|	ПС_ОбработкаДокументов.Распознан КАК Распознан,
	|	ПС_ОбработкаДокументов.Найден КАК Найден,
	|	ПС_ОбработкаДокументов.Прикреплен КАК Прикреплен,
	|	ПС_ОбработкаДокументов.КоличествоПопыток КАК КоличествоПопыток,
	|	ПС_ОбработкаДокументов.ИзвлеченныйТекст КАК ИзвлеченныйТекст,
	|	ПС_ОбработкаДокументов.Вероятность КАК Вероятность,
	|	ПС_ОбработкаДокументов.КлючевыеРеквизиты КАК КлючевыеРеквизиты,
	|	ПС_ОбработкаДокументов.ОбработкаЗавершена КАК ОбработкаЗавершена,
	|	ПС_ОбработкаДокументов.ИмяФайла КАК ИмяФайла,
	|	ПС_ОбработкаДокументов.НаКлиенте КАК НаКлиенте,
	|	ПС_ОбработкаДокументов.ПредставлениеДокумента КАК ПредставлениеДокумента,
	|	ПС_ОбработкаДокументов.ТипДокумента КАК ТипДокумента,
	|	ПС_ОбработкаДокументов.КомментарийОбработки КАК КомментарийОбработки
	|ИЗ
	|	РегистрСведений.ПС_ОбработкаДокументов КАК ПС_ОбработкаДокументов
	|ГДЕ
	|	ПС_ОбработкаДокументов.ХешСумма = &ХешСумма
	|	И НЕ ПС_ОбработкаДокументов.ОбработкаЗавершена";
	
	Запрос.УстановитьПараметр("ХешСумма", ХешСумма);
	ТаблицаЗначений = Запрос.Выполнить().Выгрузить();	
	
	Результат = Новый Массив;
	
	СтруктураСтрока = Новый Структура;
	
	Для каждого ТекКолонка Из ТаблицаЗначений.Колонки Цикл
		
		СтруктураСтрока.Вставить(ТекКолонка.Имя);
		
	КонецЦикла;
	
	Для каждого ТекСтрока Из ТаблицаЗначений Цикл
		
		ЗаполнитьЗначенияСвойств(СтруктураСтрока, ТекСтрока);
		Результат.Добавить(СтруктураСтрока);
		
	КонецЦикла;	
	
	Возврат Результат;
	
КонецФункции

// Возвращает массив каталогов
//Параметры:
// КаталогиНаКлиенте - булево
// Возвращает массив Обрабатываемых каталогов с полями:
//	ПутьНаСервере - строка
//	НаКлиенте - булево
//Возвращаемое значение:
//	Тип: массив структур
Функция ПолучитьИндексируемыеКаталоги(КаталогиНаКлиенте = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПС_КаталогиПотоковогоСканирования.Ссылка КАК Ссылка,
		|	ПС_КаталогиПотоковогоСканирования.ПутьККаталогу КАК ПутьККаталогу,
		|	ПС_КаталогиПотоковогоСканирования.НаКлиенте КАК НаКлиенте
		|ИЗ
		|	Справочник.ПС_КаталогиПотоковогоСканирования КАК ПС_КаталогиПотоковогоСканирования
		|ГДЕ
		|	ПС_КаталогиПотоковогоСканирования.Активен
		|	И
		|		ПС_КаталогиПотоковогоСканирования.Ссылка <> ЗНАЧЕНИЕ(Справочник.ПС_КаталогиПотоковогоСканирования.КаталогДляОбработанныхДокументов)
		|	И (&КаталогиНаКлиенте = НЕОПРЕДЕЛЕНО
		|	ИЛИ ПС_КаталогиПотоковогоСканирования.НаКлиенте = &КаталогиНаКлиенте)
		|
		|УПОРЯДОЧИТЬ ПО
		|	НаКлиенте";
	
	Запрос.УстановитьПараметр("КаталогиНаКлиенте", КаталогиНаКлиенте);
	
	ВыборкаКаталогов = Запрос.Выполнить().Выбрать();
	
	Результат = Новый Массив;
	
	Пока ВыборкаКаталогов.Следующий() Цикл
		
		СтруктураКаталога = Новый Структура;
		СтруктураКаталога.Вставить("ПутьККаталогу", ВыборкаКаталогов.ПутьККаталогу);
		СтруктураКаталога.Вставить("НаКлиенте", ВыборкаКаталогов.НаКлиенте);
		
		Результат.Добавить(СтруктураКаталога);
		
	КонецЦикла;
	
	Возврат Результат;	
		
КонецФункции

//Перебирает файлы в каталогах сканирования и заносит в регистр
Процедура ПроверитьКаталогиСФайлами() Экспорт
	
	МассивКаталогов = ПолучитьИндексируемыеКаталоги(Ложь);
	
	Для каждого ВыборкаКаталогов Из МассивКаталогов Цикл
		
		ПутьККаталогу = ВыборкаКаталогов.ПутьККаталогу;		
		КаталогНаСервере = Новый Файл(ПутьККаталогу);
		
		Если НЕ КаталогНаСервере.Существует() Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		МассивФайлов = НайтиФайлы(ПутьККаталогу, "*.*", Истина);
		
		Для Каждого Файл Из МассивФайлов Цикл
			
			Если ЭтоИзображениеИлиPDF(Файл.Расширение) Тогда
	
				ХешСумма = ПолучитьХешСуммуФайла(Файл.ПолноеИмя);

				ДобавитьФайлВРегистр(Файл.ПолноеИмя, ХешСумма);

			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

//Добавляет файл в регистр
//Параметры:
//	ПутьКФайлу - строка
//	ХешСумма - строка
//	ФайлНаКлиенте - булево
Процедура ДобавитьФайлВРегистр(ПутьКФайлу, ХешСумма, ФайлНаКлиенте = Ложь) Экспорт

	НаборЗаписей = РегистрыСведений.ПС_ОбработкаДокументов.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ХешСумма.Установить(ХешСумма);
	НаборЗаписей.Прочитать();
				
	Если НаборЗаписей.Количество() = 0 Тогда
				
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.Период = ТекущаяДатаСеанса();
		НоваяЗапись.ХешСумма = ХешСумма;
		НоваяЗапись.ИмяФайла = ПутьКФайлу;
		НоваяЗапись.НаКлиенте = ФайлНаКлиенте;
					
		НаборЗаписей.Записать();
					
	КонецЕсли;	
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПрикрепитьСканДокументаНаСервере(ЗаписьРегистра, АдресХранилищаИлиПуть) Экспорт
	
	Если ЭтоАдресВременногоХранилища(АдресХранилищаИлиПуть) Тогда
	
		АдресВХранилище = АдресХранилищаИлиПуть;
		
	Иначе
	
		АдресВХранилище = ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(АдресХранилищаИлиПуть));
		
	КонецЕсли;
	
	ПараметрыФайла = РаботаСФайлами.ПараметрыДобавленияФайла();
	ПараметрыФайла.ИмяБезРасширения = ЗаписьРегистра.ПредставлениеДокумента;
	ПараметрыФайла.РасширениеБезТочки = "pdf";
	ПараметрыФайла.ВладелецФайлов = ЗаписьРегистра.Объект;
	
	Попытка
	
		СсылкаНаФайл = РаботаСФайлами.ДобавитьФайл(ПараметрыФайла,
		АдресВХранилище);
		
		Если НЕ СсылкаНаФайл.Пустая() Тогда
			
			Результат = Истина;
			ЗаписьРегистра.Прикреплен = Истина;
			ЗаписьРегистра.КомментарийОбработки = ЗаписьРегистра.КомментарийОбработки 
			+ ?(ЗаписьРегистра.КомментарийОбработки  = "", "", Символы.ПС) + "Файл прикреплен к докумкенту успешно";			
			
		КонецЕсли;
		
	
		
	Исключение

		ЗаписьРегистра.КомментарийОбработки = ЗаписьРегистра.КомментарийОбработки 
		+ ?(ЗаписьРегистра.КомментарийОбработки  = "", "", Символы.ПС) + "Не удалось прикрепить файл к документу";			
		
	КонецПопытки;
	
	
КонецПроцедуры

Процедура ЗавершитьОбработкуДокументаНаСервере(ЗаписьРегистра) Экспорт
		
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПС_ПравилаЗавершенияОбработкиДокументовРасширение.ИмяРеквизита КАК ИмяРеквизита,
		|	ПС_ПравилаЗавершенияОбработкиДокументовРасширение.ФормулаРасчетаЗначения КАК ФормулаРасчетаЗначения,
		|	ПС_ПравилаЗавершенияОбработкиДокументовРасширение.БСП КАК БСП
		|ИЗ
		|	РегистрСведений.ПС_ПравилаЗавершенияОбработкиДокументов КАК ПС_ПравилаЗавершенияОбработкиДокументовРасширение
		|ГДЕ
		|	ПС_ПравилаЗавершенияОбработкиДокументовРасширение.Активно
		|	И ПС_ПравилаЗавершенияОбработкиДокументовРасширение.ТипДокумента = &ТипДокумента";
	
	Запрос.Параметры.Вставить("ТипДокумента", ЗаписьРегистра.ТипДокумента);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ДокОбъект = ЗаписьРегистра.Объект.ПолучитьОбъект();
	
	Пока Выборка.Следующий() Цикл
		
		Объект = ЗаписьРегистра;

		УстановитьБезопасныйРежим(Истина);			
		Значение = Вычислить(Выборка.ФормулаРасчетаЗначения);
		УстановитьБезопасныйРежим(Ложь);		
		
		Если Выборка.БСП Тогда
		
			СвВо = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", Выборка.ИмяРеквизита);
			
			Найденный = ДокОбъект.ДополнительныеРеквизиты.Найти(СвВо, "Свойство");
			
			Если Найденный = Неопределено Тогда
				
				Найденный = ДокОбъект.ДополнительныеРеквизиты.Добавить();
				Найденный.Свойство = СвВо;
				
			КонецЕсли;
			
			Найденный.Значение = Значение;
			
		Иначе
			
			ДокОбъект[Выборка.ИмяРеквизита] = Значение;			
			
		КонецЕсли;
		
	КонецЦикла;
	
	ДокОбъект.ОбменДанными.Загрузка = Истина;
	ДокОбъект.Записать(РежимЗаписиДокумента.Запись);
	
	ЗаписьРегистра.ОбработкаЗавершена = Истина;
	ЗаписьРегистра.ОкончаниеОбработки = ТекущаяДатаСеанса();
	
КонецПроцедуры

Функция ПолучитьСконвертированныйСканНаСервере(ИмяФайла)

	Слэш = ?(ЭтоСерверLinux(), "/", "\");

	//Получаем компоненту конвертации
	КаталогКомпоненты = КаталогВременныхФайлов() + "PSTools";
	
	КаталогНаКлиенте = Новый Файл(КаталогКомпоненты);
	
	Если НЕ КаталогНаКлиенте.Существует() Тогда
		
		СоздатьКаталог(КаталогКомпоненты);
		
	КонецЕсли;
	
	ИмяФайлаКомпоненты = "PdfToStream.dll";
	
	СисИнфо = Новый СистемнаяИнформация;
	
	ПолноеИмяФайлаКомпоненты = КаталогКомпоненты + Слэш + ИмяФайлаКомпоненты;
	
	Если НайтиФайлы(КаталогКомпоненты, ИмяФайлаКомпоненты).Количество() = 0 Тогда
	
		ДД_Компоненты = ПолучитьКомпонентыКонвертацииССервера(СисИнфо.ТипПлатформы, Ложь);	
		
		ДД_Компоненты.Записать(ПолноеИмяФайлаКомпоненты);
		
	КонецЕсли;
	
	МассивДанных = Неопределено;
	
	Если ПодключитьВнешнююКомпоненту(ПолноеИмяФайлаКомпоненты,"VK",ТипВнешнейКомпоненты.Native) Тогда  
	
		Компонента = Новый("AddIn.VK.PdfToStream");
		Результат = Компонента.ЗагрузитьPDF(ИмяФайла);

		Если Результат Тогда
				
			МассивДанных = Новый Массив;
			
			КоличествоСтраниц = Компонента.КоличествоСтраницВДокументе;
			
			Для Ы = 0 По КоличествоСтраниц -1 Цикл	
			
				КартинкаДокумента = Компонента.ПолучитьСтраницу(Ы);
				ТекстДокумента = Компонента.ПолучитьТекстСтраницы(Ы);
				СтруктураДанных = Новый Структура("Картинка, Текст", КартинкаДокумента, ТекстДокумента);
			
				МассивДанных.Вставить(СтруктураДанных);
				
			КонецЦикла;			

		КонецЕсли;
						
	Иначе
							
		СообщениеПользователю = Новый СообщениеПользователю;
		СообщениеПользователю.Текст = "Не удалось подключить компоненту на сервере";
		СообщениеПользователю.Сообщить();							

	КонецЕсли;
	
	Возврат МассивДанных;
	
КонецФункции

Процедура РаспознатьДокумент(ЗаписьРегистра, АдресФайлаНаКлиенте = "")
	
	//РезультатУспешноРаспознан = Ложь;
	
	НужноУдалитьФайл = Ложь;
	
	Если ЗаписьРегистра.НаКлиенте И ЭтоАдресВременногоХранилища(АдресФайлаНаКлиенте) Тогда
		
		ДанныеИзХранилища = ПолучитьИзВременногоХранилища(АдресФайлаНаКлиенте);
		
		Если ТипЗнч(ДанныеИзХранилища) = Тип("Массив") Тогда //PDF сконвертирован на клиенте
		
			ДокументСОбрамлением = ПереупаковатьPDF(ДанныеИзХранилища);
			
			ИмяВременногоФайла = ПолучитьИмяВременногоФайла("pdf");	
			ДокументСОбрамлением.Записать(ИмяВременногоФайла, ТипФайлаТабличногоДокумента.PDF);
		
		ИначеЕсли ТипЗнч(ДанныеИзХранилища) = Тип("Структура") И ДанныеИзХранилища.Свойство("БылаПопыткаКонвертации") Тогда 
		
			ДокументБезОбрамления = ДанныеИзХранилища.ДвоичныеДанныеФайлаНаКлиенте;
			ИмяВременногоФайла = ПолучитьИмяВременногоФайла("pdf");	
			ДокументБезОбрамления.Записать(ИмяВременногоФайла);

			ПолноеИмяФайла = ИмяВременногоФайла;			
			
		Иначе //Сохраняем и конвертируем на сервере, т.к. предполагается, что это win сервер, иначе пришел бы массив
		
			Если ПолучитьНастройкуРаспознавания("ИспользоватьКомпонентуОкантовки") = Истина Тогда
		
				ИмяВременногоФайла = ПолучитьИмяВременногоФайла("pdf");
				ДвоичныеДанные = ДанныеИзХранилища;
				ДвоичныеДанные.Записать(ИмяВременногоФайла);				
				
				ДД_PDF = ПолучитьСконвертированныйСканНаСервере(ИмяВременногоФайла);
				
				Если ДД_PDF <> Неопределено Тогда
					
					УдалитьФайлы(ИмяВременногоФайла);
					
					ДокументСОбрамлением = ПереупаковатьPDF(ДД_PDF);
					
					ИмяВременногоФайла = ПолучитьИмяВременногоФайла("pdf");	
					ДокументСОбрамлением.Записать(ИмяВременногоФайла, ТипФайлаТабличногоДокумента.PDF);
					
				КонецЕсли;
				
				ПолноеИмяФайла = ИмяВременногоФайла;
				
			Иначе	
							
				ИмяВременногоФайла = ПолучитьИмяВременногоФайла("pdf");	
				ДанныеИзХранилища.Записать(ИмяВременногоФайла);	
				ПолноеИмяФайла = ИмяВременногоФайла;
											
			КонецЕсли;
			
		КонецЕсли;
		
		НужноУдалитьФайл = Истина;
		
	Иначе

		ПолноеИмяФайла = ЗаписьРегистра.ИмяФайла;
		
		Если ПолучитьНастройкуРаспознавания("ИспользоватьКомпонентуОкантовки") = Истина Тогда
		
			ДД_PDF = ПолучитьСконвертированныйСканНаСервере(ПолноеИмяФайла);
				
			Если ДД_PDF = Неопределено Тогда
					
				Возврат;
					
			КонецЕсли;				
				
			ДокументСОбрамлением = ПереупаковатьPDF(ДД_PDF);
				
			ИмяВременногоФайла = ПолучитьИмяВременногоФайла("pdf");	
			ДокументСОбрамлением.Записать(ИмяВременногоФайла, ТипФайлаТабличногоДокумента.PDF);
	
			ПолноеИмяФайла = ИмяВременногоФайла;
			
			НужноУдалитьФайл = Истина;
			
		КонецЕсли;
				
	КонецЕсли;
	
	ДДСкана = Новый ДвоичныеДанные(ПолноеИмяФайла);
		
	//Отправка в N8N		
	РезультатОбработки = РаспознатьДокументN8N(ДДСкана);
		
	Если РезультатОбработки.ОбработанУспешно Тогда
			
			ЗаписьРегистра.ИзвлеченныйТекст = СтруктураВJSON(РезультатОбработки.РаспознанныеДанные);
			ЗаписьРегистра.КлючевыеРеквизиты = СтруктураВJSON(РезультатОбработки.КлючевыеРеквизиты);
			
			ЗаписьРегистра.Распознан = Истина;
			//РезультатУспешноРаспознан = Истина; 
			
	КонецЕсли;
		
	ЗаписьРегистра.КомментарийОбработки = ЗаписьРегистра.КомментарийОбработки 
	+ ?(ЗаписьРегистра.КомментарийОбработки  = "", "", Символы.ПС) + РезультатОбработки.Комментарий;
	
	//Удаляем только временный файл, переписанный с клиента.
	//Если это исходный файл, его не трогаем
	Если НужноУдалитьФайл Тогда
		УдалитьФайлы(ИмяВременногоФайла);
	КонецЕсли;	
	
	Возврат;
	
КонецПроцедуры

Процедура НайтиСвязиПоКлючевымРеквизитам(ЗаписьРегистра)
	
	ТочностьСтрогогоПоиска = 100;
	ТочностьПоискаИИ = 0;
	БылПоискИИ = Ложь;
	
	СтруктураКлючевыхРеквизитов = JSONВСтруктуру(ЗаписьРегистра.КлючевыеРеквизиты);
	
	ОрганизацияЭтоПоставщик = Ложь;
	ЗаписьРегистра.Организация = Неопределено;
	ЗаписьРегистра.Контрагент = Неопределено;
	
	ОпределитьОрганизацию(ЗаписьРегистра, СокрЛП(СтруктураКлючевыхРеквизитов.INN_company), ТочностьСтрогогоПоиска, ОрганизацияЭтоПоставщик, Истина);
	ОпределитьОрганизацию(ЗаписьРегистра, СокрЛП(СтруктураКлючевыхРеквизитов.INN_client), ТочностьСтрогогоПоиска);

	ОпределитьКонтрагента(ЗаписьРегистра, СокрЛП(СтруктураКлючевыхРеквизитов.INN_client), ТочностьСтрогогоПоиска, ОрганизацияЭтоПоставщик, Истина);
	ОпределитьКонтрагента(ЗаписьРегистра, СокрЛП(СтруктураКлючевыхРеквизитов.INN_company), ТочностьСтрогогоПоиска);
	
	//Тип документа
	Если СтруктураКлючевыхРеквизитов.doc_type = "Акт" Тогда
	
		Если ОрганизацияЭтоПоставщик Тогда
			
			ЗаписьРегистра.ТипДокумента = Перечисления.ПС_ТипыРаспознанныхДокументов.РеализацияТоваровИУслуг;
			
		Иначе
						
			ЗаписьРегистра.ТипДокумента = Перечисления.ПС_ТипыРаспознанныхДокументов.ПоступлениеТоваровИУслуг;
			
		КонецЕсли;
		
	ИначеЕсли СтруктураКлючевыхРеквизитов.doc_type = "Счет" Тогда
	
		ЗаписьРегистра.ТипДокумента = Перечисления.ПС_ТипыРаспознанныхДокументов.Счет;
		
	ИначеЕсли СтруктураКлючевыхРеквизитов.doc_type = "Счет-Фактура" Тогда
		
		Если ОрганизацияЭтоПоставщик Тогда
			
			ЗаписьРегистра.ТипДокумента = Перечисления.ПС_ТипыРаспознанныхДокументов.СчетФактураИсходящий;
			
		Иначе
						
			ЗаписьРегистра.ТипДокумента = Перечисления.ПС_ТипыРаспознанныхДокументов.СчетФактураВходящий;
			
		КонецЕсли;	
			
	КонецЕсли;
	
	ЗаписьРегистра.Номер = СтруктураКлючевыхРеквизитов.number;
	ЗаписьРегистра.Дата = XMLЗначение(Тип("Дата"), СтруктураКлючевыхРеквизитов.date);
	
	ЗаписьРегистра.ТипСодержимого = СтруктураКлючевыхРеквизитов.content_type;
	ЗаписьРегистра.КраткоеСодержание = СтруктураКлючевыхРеквизитов.content_min;
	
	ЗаписьРегистра.ПредставлениеДокумента = СтрШаблон("%1 (%2) №%3 от %4 (%5 - %6)", 
	ЗаписьРегистра.ТипДокумента,
	ЗаписьРегистра.ТипСодержимого,
	ЗаписьРегистра.Номер,
	Формат(ЗаписьРегистра.Дата, "ДФ=dd.MM.yyyy;"),
	ЗаписьРегистра.Организация,
	ЗаписьРегистра.Контрагент);
	
	ЗаписьРегистра.Сумма = Число2(СтруктураКлючевыхРеквизитов.summ);
	
	Если ОрганизацияЭтоПоставщик Тогда
	
		ИННОрганизации = СтруктураКлючевыхРеквизитов.INN_company;
		ИННКонтрагента = СтруктураКлючевыхРеквизитов.INN_client;
		
	Иначе
		
		ИННОрганизации = СтруктураКлючевыхРеквизитов.INN_client;
		ИННКонтрагента = СтруктураКлючевыхРеквизитов.INN_company;
		
	КонецЕсли;
	
	Если ПустаяСтрока(ИННОрганизации) ИЛИ ПустаяСтрока(ИННКонтрагента) Тогда
			
		ТекстКомментария = "Не определены ИНН сторон документа, жесткий поиск невозможен";			
		ЗаписьРегистра.КомментарийОбработки = ?(ПустаяСтрока(ЗаписьРегистра.КомментарийОбработки), ТекстКомментария, ЗаписьРегистра.КомментарийОбработки + Символы.ПС + ТекстКомментария);	
		ТочностьСтрогогоПоиска = 0;
	
	Иначе
	
		НайтиДокументИБ(ЗаписьРегистра, ТочностьСтрогогоПоиска, ИННОрганизации, ИННКонтрагента);
	
	КонецЕсли;
	
	ПорогПоискаИИ = НастройкаРаспознаванияЧисло("ПорогТочностиДляДополнительногоПоискаИИ", 100);
	
	Если ТочностьСтрогогоПоиска < ПорогПоискаИИ Тогда
	
		СтруктураКлючевыхРеквизитов.INN_company = ИННОрганизации;
		СтруктураКлючевыхРеквизитов.INN_client = ИННКонтрагента;
	
		НайтиДокументСПрименениемИИ(ЗаписьРегистра, ТочностьПоискаИИ, ТочностьСтрогогоПоиска, СтруктураКлючевыхРеквизитов);
		БылПоискИИ = Истина;
		
	ИначеЕсли ЗначениеЗаполнено(ЗаписьРегистра.Объект) Тогда //Получается, нашли
		
		ЗаписьРегистра.Найден = Истина;
		
	КонецЕсли;
	
	ЗаписьРегистра.Вероятность = Макс(ТочностьПоискаИИ, ТочностьСтрогогоПоиска);
	
КонецПроцедуры

Функция НастройкаРаспознаванияЧисло(ИмяНастройки, ПоУмолчанию = 0)
	
	ТекЗначение = ПолучитьНастройкуРаспознавания(ИмяНастройки);
	
	Результат = ПоУмолчанию;
	
	Если ТипЗнч(ТекЗначение) = Тип("Число") Тогда
		
		Результат = ТекЗначение;
		
	КонецЕсли;
	
	Возврат Результат;
		
КонецФункции

Процедура НайтиДокументСПрименениемИИ(ЗаписьРегистра, ТочностьПоискаИИ, ТочностьСтрогогоПоиска, СтруктураКлючевыхРеквизитов)

	НайденныйДокумент = Неопределено;
	
	ИмяТаблицыДокумента = ИмяТаблицыПоТипуДокумента(ЗаписьРегистра.ТипДокумента);
	ИменаПолейДатаНомер = ИменаПолейДатаНомер(ЗаписьРегистра.ТипДокумента);

	ГлубинаИИПоискаМес = НастройкаРаспознаванияЧисло("ГлубинаИИПоискаМес", 12);
	КоличествоСтрокДляПоискаДокументаИИ = НастройкаРаспознаванияЧисло("КоличествоСтрокДляПоискаДокументаИИ", 1);

	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ВЫБОР
		|		КОГДА Документ.%2 = ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА Документ.Дата
		|		ИНАЧЕ Документ.%2
		|	КОНЕЦ КАК Дата,
		|	ВЫБОР
		|		КОГДА Документ.%3 = """"
		|			ТОГДА Документ.Номер
		|		ИНАЧЕ Документ.%3
		|	КОНЕЦ КАК Номер,
		|	Документ.Организация.ИНН КАК ИННОрганизации,
		|	Документ.Контрагент.ИНН КАК ИННКонтрагента,
		|	Документ.СуммаДокумента КАК Сумма,
		|	ДокументТовары.Номенклатура.Наименование КАК Содержание
		|ИЗ
		|	Документ.%1 КАК Документ
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.%1.Товары КАК ДокументТовары
		|		ПО Документ.Ссылка = ДокументТовары.Ссылка
		|		И ДокументТовары.НомерСтроки <= &КоличествоСтрокДляПоискаДокументаИИ
		|ГДЕ
		|	Документ.Организация.ИНН = &ИННОрганизации
		|	И Документ.Контрагент.ИНН = &ИННКонтрагента
		|	И (Документ.%2 МЕЖДУ &НачалоИнтервала И &КонецИнтервала
		|	ИЛИ Документ.Дата МЕЖДУ &НачалоИнтервала И &КонецИнтервала)";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%1", ИмяТаблицыДокумента);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%2", ИменаПолейДатаНомер.ИмяДата);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%3", ИменаПолейДатаНомер.ИмяНомер);
	Запрос.УстановитьПараметр("ИННОрганизации", СтруктураКлючевыхРеквизитов.INN_company);
	Запрос.УстановитьПараметр("ИННКонтрагента", СтруктураКлючевыхРеквизитов.INN_client);
	Запрос.УстановитьПараметр("КоличествоСтрокДляПоискаДокументаИИ", КоличествоСтрокДляПоискаДокументаИИ);
	Запрос.УстановитьПараметр("НачалоИнтервала", ДобавитьМесяц(НачалоГода(ЗаписьРегистра.Дата), -1 * ГлубинаИИПоискаМес)); 
	Запрос.УстановитьПараметр("КонецИнтервала", КонецГода(ЗаписьРегистра.Дата));

	МассивВариантов = Новый Массив;
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаРезультатов = РезультатЗапроса.Выбрать();
	
	Если ВыборкаРезультатов.Количество() > 0 Тогда

		Пока ВыборкаРезультатов.Следующий() Цикл
			
			НовыйВариант = Новый Структура;
			НовыйВариант.Вставить("date", 			ВыборкаРезультатов.Дата);
			НовыйВариант.Вставить("number", 		ВыборкаРезультатов.Номер);
			НовыйВариант.Вставить("INN_company", 	ВыборкаРезультатов.ИННОрганизации);
			НовыйВариант.Вставить("INN_client", 	ВыборкаРезультатов.ИННКонтрагента);
			НовыйВариант.Вставить("summ", 			ВыборкаРезультатов.Сумма);
			НовыйВариант.Вставить("content_min", 	ВыборкаРезультатов.Содержание);
			
			МассивВариантов.Добавить(НовыйВариант);
			
		КонецЦикла;

		РезультатПоиска = ПоискДокументаN8N(ЗаписьРегистра, СтруктураКлючевыхРеквизитов, МассивВариантов);
		
		Если РезультатПоиска.ОбработанУспешно Тогда
	
			ТочностьПоискаИИ = РезультатПоиска.Результат.accuracy;
	
			ЗаписьРегистра.КомментарийОбработки = ЗаписьРегистра.КомментарийОбработки 
			+ ?(ЗаписьРегистра.КомментарийОбработки  = "", "", Символы.ПС) + "ИИ нашел документ в базе с вероятностью " + Формат(ТочностьПоискаИИ, "ЧДЦ=2;") + "%" + Символы.ПС
			+ "Комментарий ИИ: " + РезультатПоиска.Результат.comment;
			
			Если ТочностьПоискаИИ >= ТочностьСтрогогоПоиска Тогда
			
				НайденныйДокумент = Документы[ИмяТаблицыПоТипуДокумента(ЗаписьРегистра.ТипДокумента)].НайтиПоНомеру(РезультатПоиска.Результат.number, РезультатПоиска.Результат.date);
			
				Если НЕ НайденныйДокумент.Пустая() Тогда
				
				
					ЗаписьРегистра.КомментарийОбработки = ЗаписьРегистра.КомментарийОбработки 
					+ ?(ЗаписьРегистра.КомментарийОбработки  = "", "", Символы.ПС) + "Точность поиска ИИ оказалась больше точности обычного поиска, будет принята версия ИИ";
				
					ЗаписьРегистра.Объект = НайденныйДокумент;
					ЗаписьРегистра.Найден = Истина;
					
				КонецЕсли;
				
			Иначе
				
				ЗаписьРегистра.КомментарийОбработки = ЗаписьРегистра.КомментарийОбработки 
				+ ?(ЗаписьРегистра.КомментарийОбработки  = "", "", Символы.ПС) + "Точность поиска ИИ оказалась меньше ожидаемой, будет принята версия обычного поиска 1С";
				
				Если ЗначениеЗаполнено(ЗаписьРегистра.Объект) Тогда
					
					ЗаписьРегистра.Найден = Истина;
					
				КонецЕсли;
				
			КонецЕсли;
			
		Иначе
			
			ЗаписьРегистра.КомментарийОбработки = ЗаписьРегистра.КомментарийОбработки 
			+ ?(ЗаписьРегистра.КомментарийОбработки  = "", "", Символы.ПС) + "Не удалось выполнить поисковый запрос к ИИ " + РезультатПоиска.Комментарий;						
			
		КонецЕсли;
		
	Иначе
		
		ЗаписьРегистра.КомментарийОбработки = ЗаписьРегистра.КомментарийОбработки 
		+ ?(ЗаписьРегистра.КомментарийОбработки  = "", "", Символы.ПС) + "В базе не найдено никаких вариантов документов по указанным ИНН, которые бы можно было предложить для поиска ИИ";
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПоискДокументаN8N(ЗаписьРегистра, СтруктураКлючевыхРеквизитов, МассивВариантов)
	
	РезультатОбработки = Новый Структура;
	РезультатОбработки.Вставить("ОбработанУспешно", Ложь);
	РезультатОбработки.Вставить("Результат", Новый Структура);
	РезультатОбработки.Вставить("Комментарий", "");
	
	Логин = Справочники.ПС_НастройкиРаспознавания.ПараметрРаспознавания("ЛогинN8N");
	Пароль = Справочники.ПС_НастройкиРаспознавания.ПараметрРаспознавания("ПарольN8N");

	Аутентификация = ПС_КоннекторHTTP.НоваяАутентификацияBasic(Логин, Пароль);
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("x-client-id", "");
	    
	// Определение структуры
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("model", "search");
	    
	Сообщения = Новый Массив;
	    
	СообщениеСистемы = Новый Структура;
	СообщениеСистемы.Вставить("request", СтруктураКлючевыхРеквизитов);
	СообщениеСистемы.Вставить("variants", МассивВариантов);
	Сообщения.Add(СообщениеСистемы);
	    
	СтруктураДанных.Вставить("messages", Сообщения);
	
	АдресСервисаРаспознаванияДокументов = Справочники.ПС_НастройкиРаспознавания.ПараметрРаспознавания("АдресСервисаРаспознаванияДокументов");

	Попытка

		Результат = ПС_КоннекторHTTP.PostJSON(АдресСервисаРаспознаванияДокументов, СтруктураДанных, 
		Новый Структура("Аутентификация,Заголовки,Таймаут", Аутентификация, Заголовки, 120));
	
		ЕстьДанные = Ложь;

		Для каждого ТекЭлемент Из Результат["output"] Цикл
		
			РезультатОбработки.Результат.Вставить(ТекЭлемент.Ключ, ТекЭлемент.Значение);
			
			ЕстьДанные = Истина;
	
		КонецЦикла;	
		
		Если ЕстьДанные Тогда
			
			РезультатОбработки.Результат.accuracy = Число2(РезультатОбработки.Результат.accuracy);
			РезультатОбработки.Результат.date = XMLЗначение(Тип("Дата"), СтруктураКлючевыхРеквизитов.date);
		
			РезультатОбработки.ОбработанУспешно = Истина;
			
		КонецЕсли;
		
	Исключение
		
		РезультатОбработки.Комментарий = "Ошибка: " + ОписаниеОшибки();
		
	КонецПопытки;
	
	Возврат РезультатОбработки;
	
КонецФункции

Функция ИмяТаблицыПоТипуДокумента(ТипДокумента)
	
	//!!! Тут нужно возвращать массив и если в массиве более 1 элемента, составлять динамически запрос.
	//Т.к. может быть поступление услуг и прочих активов и обычное поступлениие.
	
	Если ТипДокумента = Перечисления.ПС_ТипыРаспознанныхДокументов.ПоступлениеТоваровИУслуг Тогда
		
		Возврат "ПриобретениеТоваровУслуг";
		
	ИначеЕсли ТипДокумента = Перечисления.ПС_ТипыРаспознанныхДокументов.РеализацияТоваровИУслуг Тогда
		
		Возврат "РеализацияТоваровУслуг";
		
	КонецЕсли;
	
КонецФункции

Функция ИменаПолейДатаНомер(ТипДокумента)
	
	Если ТипДокумента = Перечисления.ПС_ТипыРаспознанныхДокументов.ПоступлениеТоваровИУслуг Тогда
		
		Возврат Новый Структура("ИмяДата,ИмяНомер", "ДатаВходящегоДокумента", "НомерВходящегоДокумента");
		
	ИначеЕсли ТипДокумента = Перечисления.ПС_ТипыРаспознанныхДокументов.РеализацияТоваровИУслуг Тогда
		
		Возврат Новый Структура("ИмяДата,ИмяНомер", "Дата", "Номер");
		
	КонецЕсли;
	
КонецФункции

Процедура НайтиДокументИБ(ЗаписьРегистра, ТочностьСтрогогоПоиска, ИННОрганизации, ИННКонтрагента)

	//!!! В перспективе переделать все запросы на динамическое создание какой-то одной функцией

	ТочностьПоиска = 0;
	НайденныйДокумент = Неопределено;
	КомментарийАлгоритма = "";
	
	ИмяТаблицыДокумента = ИмяТаблицыПоТипуДокумента(ЗаписьРегистра.ТипДокумента);
	ИменаПолейДатаНомер = ИменаПолейДатаНомер(ЗаписьРегистра.ТипДокумента);

	// --- Шаг 1: Точный поиск по всем полям ---
	// Если в базе найден документ, по всем переданным параметрам, то точность = 100.
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Документ.Ссылка
		|ИЗ
		|	Документ.%1 КАК Документ
		|ГДЕ
		|	Документ.Организация = &Организация
		|	И Документ.Контрагент = &Контрагент
		|	И (Документ.Дата Между &НачалоДня И &КонецДня ИЛИ Документ.%2 Между &НачалоДня И &КонецДня)
		|	И Документ.%3 ПОДОБНО &Номер
		|	И Документ.СуммаДокумента = &Сумма";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%1", ИмяТаблицыДокумента);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%2", ИменаПолейДатаНомер.ИмяДата);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%3", ИменаПолейДатаНомер.ИмяНомер);
	Запрос.УстановитьПараметр("Организация", ЗаписьРегистра.Организация);
	Запрос.УстановитьПараметр("Контрагент", ЗаписьРегистра.Контрагент);
	Запрос.УстановитьПараметр("НачалоДня", НачалоДня(ЗаписьРегистра.Дата));
	Запрос.УстановитьПараметр("КонецДня", КонецДня(ЗаписьРегистра.Дата));
	Запрос.УстановитьПараметр("Номер", ЗаписьРегистра.Номер);
	Запрос.УстановитьПараметр("Сумма", ЗаписьРегистра.Сумма);

	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		НайденныйДокумент = Выборка.Ссылка;
		ТочностьПоиска = 100;
		КомментарийАлгоритма = "Найден документ по полному набору реквизитов. Точность: 100%";
		
	КонецЕсли;

	// --- Шаг 2: Поиск по ИНН (полный набор данных) ---
	// Если не найден, нужна вторая попытка поиска, но нужно искать не по ссылкам на Контрагента и организацию, а по ИНН контрагента и ИНН организации.
	// Если найден документ, также точность 100.
	Если НайденныйДокумент = Неопределено Тогда
		
		Если НЕ ПустаяСтрока(ИННОрганизации) И НЕ ПустаяСтрока(ИННКонтрагента) Тогда
			
			Запрос = Новый Запрос;
			Запрос.Текст =
				"ВЫБРАТЬ ПЕРВЫЕ 1
				|	Документ.Ссылка
				|ИЗ
				|	Документ.%1 КАК Документ
				|ГДЕ
				|	Документ.Организация.ИНН = &ИННОрганизации
				|	И Документ.Контрагент.ИНН = &ИННКонтрагента
				|	И (Документ.Дата Между &НачалоДня И &КонецДня ИЛИ Документ.%2 Между &НачалоДня И &КонецДня)
				|	И Документ.%3 = &Номер
				|	И Документ.СуммаДокумента = &Сумма";
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "%1", ИмяТаблицыДокумента);
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "%2", ИменаПолейДатаНомер.ИмяДата);
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "%3", ИменаПолейДатаНомер.ИмяНомер);
			Запрос.УстановитьПараметр("ИННОрганизации", ИННОрганизации);
			Запрос.УстановитьПараметр("ИННКонтрагента", ИННКонтрагента);
			Запрос.УстановитьПараметр("НачалоДня", НачалоДня(ЗаписьРегистра.Дата));
			Запрос.УстановитьПараметр("КонецДня", КонецДня(ЗаписьРегистра.Дата));
			Запрос.УстановитьПараметр("Номер", ЗаписьРегистра.Номер);
			Запрос.УстановитьПараметр("Сумма", ЗаписьРегистра.Сумма);

			РезультатЗапроса = Запрос.Выполнить();
			
			Если Не РезультатЗапроса.Пустой() Тогда
				
				Выборка = РезультатЗапроса.Выбрать();
				Выборка.Следующий();
				НайденныйДокумент = Выборка.Ссылка;
				ТочностьПоиска = 100;
				КомментарийАлгоритма = "Найден документ по ИНН, дате, номеру и сумме. Точность: 100%";
				
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	// --- Шаг 3: Поиск по ИНН, дате, сумме ---
	// Если найден только один документ, то точность 90. Если найдено более одного документа, то в качестве результата поиска берем первый и точность 90/КоличествоНайденныхДокументов.
	Если НайденныйДокумент = Неопределено Тогда

		Если НЕ ПустаяСтрока(ИННОрганизации) И НЕ ПустаяСтрока(ИННКонтрагента) Тогда
			Запрос = Новый Запрос;
			Запрос.Текст =
				"ВЫБРАТЬ
				|	Документ.Ссылка
				|ИЗ
				|	Документ.%1 КАК Документ
				|ГДЕ
				|	Документ.Организация.ИНН = &ИННОрганизации
				|	И Документ.Контрагент.ИНН = &ИННКонтрагента
				|	И (Документ.Дата Между &НачалоДня И &КонецДня ИЛИ Документ.%2 Между &НачалоДня И &КонецДня)
				|	И Документ.СуммаДокумента = &Сумма";
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "%1", ИмяТаблицыДокумента);
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "%2", ИменаПолейДатаНомер.ИмяДата);			
			Запрос.УстановитьПараметр("ИННОрганизации", ИННОрганизации);
			Запрос.УстановитьПараметр("ИННКонтрагента", ИННКонтрагента);
			Запрос.УстановитьПараметр("НачалоДня", НачалоДня(ЗаписьРегистра.Дата));
			Запрос.УстановитьПараметр("КонецДня", КонецДня(ЗаписьРегистра.Дата));
			Запрос.УстановитьПараметр("Сумма", ЗаписьРегистра.Сумма);

			РезультатЗапроса = Запрос.Выполнить();
			Выборка = РезультатЗапроса.Выбрать();
			КоличествоНайденных = Выборка.Количество();
			
			Если КоличествоНайденных > 0 Тогда
				
				Выборка.Следующий();
				НайденныйДокумент = Выборка.Ссылка;
				
				Если КоличествоНайденных = 1 Тогда
					
					ТочностьПоиска = 90;
					КомментарийАлгоритма = "Найден документ по ИНН, дате и сумме. Точность: 90%";
					
				Иначе
					
					ТочностьПоиска = 90 / КоличествоНайденных;
					КомментарийАлгоритма = СтрШаблон("Найдено %1 документов по ИНН, дате и сумме. Выбран первый. Точность: %2%%", КоличествоНайденных, ТочностьПоиска);
					
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	// --- Шаг 4: Поиск по ИНН и дате (без суммы) ---
	// Если снова ничего не найдено, тогда ищем ещё раз, но без учета суммы документа и точность при этом 40/количество найденных.
	Если НайденныйДокумент = Неопределено Тогда

		Если НЕ ПустаяСтрока(ИННОрганизации) И НЕ ПустаяСтрока(ИННКонтрагента) Тогда
			
			Запрос = Новый Запрос;
			Запрос.Текст =
				"ВЫБРАТЬ
				|	Документ.Ссылка
				|ИЗ
				|	Документ.%1 КАК Документ
				|ГДЕ
				|	Документ.Организация.ИНН = &ИННОрганизации
				|	И Документ.Контрагент.ИНН = &ИННКонтрагента
				|	И (Документ.Дата Между &НачалоДня И &КонецДня ИЛИ Документ.%2 Между &НачалоДня И &КонецДня)";
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "%1", ИмяТаблицыДокумента);
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "%2", ИменаПолейДатаНомер.ИмяДата);			
			Запрос.УстановитьПараметр("ИННОрганизации", ИННОрганизации);
			Запрос.УстановитьПараметр("ИННКонтрагента", ИННКонтрагента);
			Запрос.УстановитьПараметр("НачалоДня", НачалоДня(ЗаписьРегистра.Дата));
			Запрос.УстановитьПараметр("КонецДня", КонецДня(ЗаписьРегистра.Дата));

			РезультатЗапроса = Запрос.Выполнить();
			Выборка = РезультатЗапроса.Выбрать();
			КоличествоНайденных = Выборка.Количество();
			
			Если КоличествоНайденных > 0 Тогда
				
				Выборка.Следующий();
				НайденныйДокумент = Выборка.Ссылка;
				ТочностьПоиска = 40 / КоличествоНайденных;
				КомментарийАлгоритма = СтрШаблон("Найдено %1 документов по ИНН и дате. Выбран первый. Точность: %2%%", КоличествоНайденных, ТочностьПоиска);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;

	// --- Финальная обработка результата ---
	// Если что-то нашли, записываем ссылку на документ в поле ЗаписьРегистра.Объект. 
	// В поле ЗаписьРегистра.КомментарийОбработки добавляем новую строку с указанием первого сработавшего успешно алгоритма поиска и точностью метода.
	Если НайденныйДокумент <> Неопределено Тогда
		
		ЗаписьРегистра.Объект = НайденныйДокумент;
		
		ТекстКомментария = СтрШаблон("%1 (Найден: %2, точность: %3%%)", КомментарийАлгоритма, НайденныйДокумент, Формат(ТочностьПоиска, "ЧДЦ=2"));
		
		// Добавляем новый комментарий, сохраняя старый
		Если НЕ ПустаяСтрока(ЗаписьРегистра.КомментарийОбработки) Тогда
			
			ЗаписьРегистра.КомментарийОбработки = ЗаписьРегистра.КомментарийОбработки + Символы.ПС + ТекстКомментария;
			
		Иначе
			
			ЗаписьРегистра.КомментарийОбработки = ТекстКомментария;
			
		КонецЕсли;
	
		ТочностьСтрогогоПоиска = (ТочностьСтрогогоПоиска + ТочностьПоиска) / 2;
		
	Иначе
		// Если опять ничего не найдено, тогда считаем поиск неудачным.
		ТекстКомментария = "Документ не найден ни по одному из жестких алгоритмов поиска.";
		
		Если НЕ ПустаяСтрока(ЗаписьРегистра.КомментарийОбработки) Тогда
			
			ЗаписьРегистра.КомментарийОбработки = ЗаписьРегистра.КомментарийОбработки + Символы.ПС + ТекстКомментария;
			
		Иначе
			
			ЗаписьРегистра.КомментарийОбработки = ТекстКомментария;
			
		КонецЕсли;
		
		ТочностьСтрогогоПоиска = 0;
		
	КонецЕсли;
	
	

КонецПроцедуры

Функция Число2(СуммаСтрокой)

	СуммаСтрокой = СтрЗаменить(СуммаСтрокой, ",", ".");
	
	Попытка
		
		Возврат Число(СуммаСтрокой);
		
	Исключение
	
		Возврат 0;
	
	КонецПопытки;

КонецФункции

Процедура ОпределитьОрганизацию(ЗаписьРегистра, ИНН, ТочностьСтрогогоПоиска, ФлагПоставщика = Ложь, ПроверкаНаПоставщика = Ложь)
	
	Если ЗначениеЗаполнено(ЗаписьРегистра.Организация) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	//Поиск организации
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	Организации.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.ИНН = &ИНН";
	
	Запрос.Параметры.Вставить("ИНН", ИНН);
	
	ВыборкаОрганизаций = Запрос.Выполнить().Выбрать();
	
	КоличествоНайденных = ВыборкаОрганизаций.Количество();
	
	Если КоличествоНайденных = 0 Тогда
		
		ТочностьСтрогогоПоиска = 0;
		
	ИначеЕсли КоличествоНайденных = 1 Тогда
		
		ВыборкаОрганизаций.Следующий();
		ЗаписьРегистра.Организация = ВыборкаОрганизаций.Ссылка;
		
	Иначе
		
		ВыборкаОрганизаций.Следующий();
		ЗаписьРегистра.Организация = ВыборкаОрганизаций.Ссылка;
		ТочностьСтрогогоПоиска = ТочностьСтрогогоПоиска / КоличествоНайденных;
		
		ЗаписьРегистра.КомментарийОбработки = ЗаписьРегистра.КомментарийОбработки + ?(ПустаяСтрока(ЗаписьРегистра.КомментарийОбработки), "", Символы.ПС) + 
		"Найдено " + Строка(КоличествоНайденных) + " организаций с таким ИНН. Точность поиска " + Формат(ТочностьСтрогогоПоиска, "ЧДЦ=2;");
		
	КонецЕсли;	
	
	Если КоличествоНайденных > 0 И ПроверкаНаПоставщика Тогда

		ФлагПоставщика = Истина;
			
	КонецЕсли;
		
КонецПроцедуры

Процедура ОпределитьКонтрагента(ЗаписьРегистра, ИНН, ТочностьСтрогогоПоиска, ФлагПоставщика = Ложь, ПроверкаНаКлиента = Ложь)
	
	Если ЗначениеЗаполнено(ЗаписьРегистра.Контрагент) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	//Поиск Контрагента
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	Организации.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Контрагенты КАК Организации
	|ГДЕ
	|	Организации.ИНН = &ИНН";
	
	Запрос.Параметры.Вставить("ИНН", ИНН);
	
	ВыборкаКонтрагентов = Запрос.Выполнить().Выбрать();
	
	КоличествоНайденных = ВыборкаКонтрагентов.Количество();
	
	Если КоличествоНайденных = 0 Тогда
		
		ТочностьСтрогогоПоиска = 0;
		
	ИначеЕсли КоличествоНайденных = 1 Тогда
		
		ВыборкаКонтрагентов.Следующий();
		ЗаписьРегистра.Контрагент = ВыборкаКонтрагентов.Ссылка;
		
	Иначе
		
		ВыборкаКонтрагентов.Следующий();
		ЗаписьРегистра.Контрагент = ВыборкаКонтрагентов.Ссылка;
		ТочностьСтрогогоПоиска = ТочностьСтрогогоПоиска / КоличествоНайденных;
		
		ЗаписьРегистра.КомментарийОбработки = ЗаписьРегистра.КомментарийОбработки + ?(ПустаяСтрока(ЗаписьРегистра.КомментарийОбработки), "", Символы.ПС) +
		"Найдено " + Строка(КоличествоНайденных) + " контрагентов с таким ИНН. Точность поиска " + Формат(ТочностьСтрогогоПоиска, "ЧДЦ=2;");
		
	КонецЕсли;	
	
	//Логика такая: если по переданному ИНН клиента нашли Контрагента, то вторая сторона - наша организация, т.е. поставщик
	Если НЕ ФлагПоставщика И КоличествоНайденных > 0 И ПроверкаНаКлиента Тогда

		ФлагПоставщика = Истина;
			
	КонецЕсли;
	
КонецПроцедуры

Функция СтруктураВJSON(Структура)
	
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	
	НастройкиСериализации = Новый НастройкиСериализацииJSON;
	НастройкиСериализации.СериализовыватьМассивыКакОбъекты = Ложь;
	
	ЗаписатьJSON(ЗаписьJSON, Структура, НастройкиСериализации);
	
	Возврат ЗаписьJSON.Закрыть();
	
КонецФункции

Функция JSONВСтруктуру(СтрокаJSON)
	
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(СтрокаJSON);
	
	Возврат ПрочитатьJSON(ЧтениеJSON);
	
КонецФункции


Функция ЭтоКлючевойРеквизит(ИмяРеквизита)
	
	МассивКлючевых = Новый Массив;
	МассивКлючевых.Добавить("INN_client");
	МассивКлючевых.Добавить("INN_company");
	МассивКлючевых.Добавить("date");
	МассивКлючевых.Добавить("number");
	МассивКлючевых.Добавить("doc_type");
	МассивКлючевых.Добавить("summ");
	МассивКлючевых.Добавить("content_min");
	МассивКлючевых.Добавить("content_type");
	
	Возврат МассивКлючевых.Найти(ИмяРеквизита) <> Неопределено;
	
КонецФункции

Функция РаспознатьДокументN8N(ДДСкана)
	
	РезультатОбработки = Новый Структура;
	РезультатОбработки.Вставить("ОбработанУспешно", Ложь);
	РезультатОбработки.Вставить("Комментарий", "");
	РезультатОбработки.Вставить("РаспознанныеДанные", Новый Структура);
	РезультатОбработки.Вставить("КлючевыеРеквизиты", Новый Структура);
	
	ДД_64 = Base64Строка(ДДСкана);
	
	Логин = Справочники.ПС_НастройкиРаспознавания.ПараметрРаспознавания("ЛогинN8N");
	Пароль = Справочники.ПС_НастройкиРаспознавания.ПараметрРаспознавания("ПарольN8N");

	Аутентификация = ПС_КоннекторHTTP.НоваяАутентификацияBasic(Логин, Пароль);
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("x-client-id", "");
	    
	// Определение структуры
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("model", "mistral");
	    
	Сообщения = Новый Массив;
	    
	СообщениеСистемы = Новый Структура;
	СообщениеСистемы.Вставить("document_base64", ДД_64);
	Сообщения.Add(СообщениеСистемы);
	    
	СтруктураДанных.Вставить("messages", Сообщения);
	
	АдресСервисаРаспознаванияДокументов = Справочники.ПС_НастройкиРаспознавания.ПараметрРаспознавания("АдресСервисаРаспознаванияДокументов");

	Попытка

		Результат = ПС_КоннекторHTTP.PostJSON(АдресСервисаРаспознаванияДокументов, СтруктураДанных, 
		Новый Структура("Аутентификация,Заголовки,Таймаут", Аутентификация, Заголовки, 120));
	
		ЕстьДанные = Ложь;

		Для каждого ТекЭлемент Из Результат["output"] Цикл
		
			РезультатОбработки.РаспознанныеДанные.Вставить(ТекЭлемент.Ключ, ТекЭлемент.Значение);	
			
			Если ЭтоКлючевойРеквизит(ТекЭлемент.Ключ) Тогда
			
				РезультатОбработки.КлючевыеРеквизиты.Вставить(ТекЭлемент.Ключ, ТекЭлемент.Значение);
				
			КонецЕсли;
			
			ЕстьДанные = Истина;
	
		КонецЦикла;	
		
		Если ЕстьДанные Тогда
		
			РезультатОбработки.ОбработанУспешно = Истина;
			
		КонецЕсли;
		
	Исключение
		
		РезультатОбработки.Комментарий = "Ошибка: " + ОписаниеОшибки();
		
	КонецПопытки;
	
	Возврат РезультатОбработки;
	
КонецФункции

Функция ЭтоИзображениеИлиPDF(Расширение) Экспорт
		
	// Проверяем, является ли файл изображением или PDF файлом
	МассивДопустимыхРасширений = Новый Массив;
	МассивДопустимыхРасширений.Добавить(".jpg");
	МассивДопустимыхРасширений.Добавить(".jpeg");
	МассивДопустимыхРасширений.Добавить(".png");
	МассивДопустимыхРасширений.Добавить(".pdf");
	
	Возврат МассивДопустимыхРасширений.Найти(Расширение) <> Неопределено;
	
КонецФункции

Функция ПолучитьХешСуммуФайла(Знач ПолноеИмяФайла) Экспорт
	
	НужноУдалитьФайл = Ложь;
	
	Если ЭтоАдресВременногоХранилища(ПолноеИмяФайла) Тогда
		
		ИмяВременногоФайла = ПолучитьИмяВременногоФайла();
		ДвоичныеДанные = ПолучитьИзВременногоХранилища(ПолноеИмяФайла);
		ДвоичныеДанные.Записать(ИмяВременногоФайла);
		ПолноеИмяФайла = ИмяВременногоФайла;
		
		НужноУдалитьФайл = Истина;
		
	КонецЕсли;
	
	Хеш = Новый ХешированиеДанных(ХешФункция.MD5);
	Хеш.ДобавитьФайл(ПолноеИмяФайла);
	ХешСумма = Строка(Хеш.ХешСумма);
	
	Если НужноУдалитьФайл Тогда
		УдалитьФайлы(ИмяВременногоФайла);
	КонецЕсли;
	
	Возврат ХешСумма;
	
КонецФункции

#КонецОбласти
